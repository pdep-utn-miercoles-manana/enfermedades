import enfermedades.*

describe "Enfermedades" {

	var frank
	var logan
	var malaria500 
	var malaria800
	var otitis 
	var lupus
 	var cameron 
	
	fixture {
		malaria500 = new EnfermedadInfecciosa(cantidadCelulasAmenazadas = 500)
		malaria800 = new EnfermedadInfecciosa(cantidadCelulasAmenazadas = 800)
		otitis = new EnfermedadInfecciosa(cantidadCelulasAmenazadas = 100)
		lupus = new EnfermedadAutoinmune(cantidadCelulasAmenazadas = 10000)
		
		frank = new Persona(temperatura = 36, cantidadCelulas = 3500000)
		logan = new Persona(cantidadCelulas = 3000000, temperatura = 36)
		cameron = new Medicx(cantidadCelulas = 3000000, temperatura = 36, dosis = 10)
		
		logan.contraerEnfermedad(malaria500)
		logan.contraerEnfermedad(otitis)
		logan.contraerEnfermedad(lupus)
	}

	test "PUNTO 1 - Frank sabe contraer enfermedades" {
		frank.contraerEnfermedad(malaria800)
		assert.that(frank.contrajo(malaria800))
	}
	
	test "PUNTO 2 - La malaria se sabe reproducir" {	
		malaria500.reproducirse()
		assert.equals(1000, malaria500.cantidadCelulasAmenazadas())
	}
	
	test "PUNTO 3 - Logan vive un día y pierde tempreratura" {
		logan.vivirUnDia()
		assert.equals(36.6, logan.temperatura())
	}

	test "PUNTO 3 - Logan vive un día y pierde cantidad de células" {
		logan.vivirUnDia()
		assert.equals(2990000, logan.cantidadCelulas())
	}
	
	test "PUNTO 4 - A - Cantidad celulas amenzadas por enfermedades agresivas" {
		assert.equals(0, logan.cantidadCelulasAmenazadasPorEnfermedadesAgresivas())
	}

	test "PUNTO 4 - B - Enfermedad de Logan que más células afecte" {
		assert.equals(lupus, logan.enfermedadQueMasCelulasAfecte())
	}

	test "PUNTO 4 - C - Logan sí está en coma" {
		logan = new Persona(temperatura = 45, cantidadCelulas = 999999)
		assert.that(logan.estaEnComa())
	}

	test "PUNTO 4 - C - Logan no está en coma" {
		assert.notThat(logan.estaEnComa())
	}

	// ------------//
	// TEMPORADA 2 //
	// ------------//

    test "cameron atienda a logan" {
		cameron.atenderA(logan)
		assert.equals(350, malaria500.cantidadCelulasAmenazadas())
	}
	
	// ------------//
	// TEMPORADA 3 //
	// ------------//

	test "rompe todo al donar" {
		logan = new Persona(temperatura = 36, cantidadCelulas = 1000)
		var transfusion = new Transfusion(donante = logan, receptor = frank, cantidadDeCelulas = 500)
		assert.throwsExceptionLike(
			new TransfusionException(message = "El donante no puede donar!!"), 
			{ transfusion.realizar() }
		)
	}
		
}







